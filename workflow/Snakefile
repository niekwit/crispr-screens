import pandas as pd
import datetime
include: "scripts/general_functions.smk"
from snakemake.utils import min_version, validate
from snakemake.logging import logger
from snakemake.shell import shell

# Workflow version
VERSION = "v0.7.0" 
DOCKER_VERSION = "v0.7.0" # Docker image version 

# Set minimum snakemake version
min_version("8.13.0")

# Wrapper version
wrapper_version = "v5.1.0"

logger.info(f"Workflow version: {VERSION}")
logger.info(f"Wrapper version: {wrapper_version}")

# Define report file
report: "report/workflow.rst"

# Define Docker container
containerized: f"docker://niekwit/crispr-screens:{DOCKER_VERSION}"

# Load config file
configfile: "config/config.yml"
validate(config, "schemas/config.schema.yaml")

# Load and validate stats.csv
stats = pd.read_csv("config/stats.csv")
validate(stats, "schemas/stats.schema.yaml")

# Get wildcard values
SAMPLES = sample_names()
M_COMPARISONS, B_COMPARISONS = comparisons()
CNV = cnv()
PATHWAY_DATA = pathway_data()
DBS = config["stats"]["pathway_analysis"]["dbs"]

# Load fasta
fasta = fasta()

# Import rules
include: "rules/qc.smk"
include: "rules/trim.smk"
include: "rules/count.smk"
include: "rules/stats.smk"

# Set target rule
rule all:
    input:
        targets()

# Save snakemake terminal output to log file
time = datetime.datetime.now().strftime("%Y-%m-%d_%H-%M-%S")
snake_log = f"logs/snakemake/{time}_snakemake.log"
os.makedirs("logs/snakemake", exist_ok=True)

onsuccess: 
    shell("cp -v {log} {snake_log}")
    logger.info("Analysis finished successfully!")
onerror:
    shell("cp -v {log} {snake_log}")
    logger.info(f"Analysis failed...\nCheck {snake_log} for details")